set terminal svg size 350,262 fname 'Verdana' fsize 10
set style data linespoints
set datafile separator ","

set style line 2 lc rgb '#4572a7' pt 4 ps 1 linewidth 2 

unset title
unset key

set xtics rotate by -90 
set boxwidth 0.9
set auto x

do for [name in "deadlift backsquat overheadsquat cleanandjerk snatch clean frontsquat shoulderpress pushpress sotspress pushjerk"] {
  do for [reps in "1RM 3RM 5RM"] {
    fullname = name . reps
    filename = 'data/' . fullname . ".dat"
    outfile = 'svg/' . fullname . ".svg"
    set output outfile
    plot filename using 2:xtic(3) with linespoints ls 2
 }
}

exit
